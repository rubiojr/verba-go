#!/usr/bin/env -S risor --modules lib --
/*
 * Download verba.civio.es RTVE transcripts
 */

import os
import cli
import rsx
import verba
import rsx
from cli import app, command as c

func main(basedir) {
  print('Saving data to {basedir}...')
  os.mkdir_all(filepath.join(basedir, "transcripts"))
  os.mkdir_all(filepath.join(basedir, "programmes"))

  prog := verba.programmes()
  print("Saving programmes...")
  dest_file := filepath.join(basedir, '/programmes/{time.now().unix()}.json')
  f := os.create(dest_file)
  defer f.close()
  json.marshal(prog) | f.write

  print("Saving transcripts...")
  prog.each(func(e) {
    dest_file := filepath.join(basedir, '/transcripts/{e.id}-verba-transcript.json')

    if rsx.is_file(dest_file) {
      print('Transcript for programme {e.id} exists, skipping...')
      return
    }

    print('Downloading transcript for {e.id} ({e.date})...')
    f := os.create(dest_file+ ".tmp")
    defer f.close()

    t := verba.transcription(e.id)
    json.marshal(t) | f.write
    os.rename(dest_file+ ".tmp", dest_file)
  })
}

app({
  name: "verba-download",
  description: "Download JSON transcripts from verba.civio.es",
  flags: [
    cli.flag({
      name: "dir",
      type: "string",
      value: filepath.join(getenv("HOME"), "Documents/civio/verba"),
    }),
  ],
  action: func(ctx) {
    main(ctx.string("dir"))
  }
}).run()
